buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE"
    }
}

subprojects {
    group = 'com.jugglinhats'
    version = '0.1'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    apply plugin: 'java'
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.RC2"
        }
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.projectlombok:lombok"
        compile "org.springframework.cloud:spring-cloud-starter-oauth2"
    }
}

configure([ project(':microverse-info'),
            project(':microverse-restricted'),
            project(':microverse-ui'),
            project(':microverse-gateway') ]) {
    apply plugin: 'spring-boot'

    dependencies {
        testCompile project(":microverse-test")
    }
}

project(":microverse-ui") {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    }
}

project(":microverse-gateway") {
    dependencies {
        compile "org.springframework.cloud:spring-cloud-starter-zuul"
    }

}

project(":microverse-test") {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-test"
        compile "org.springframework.security:spring-security-test"

    }
}
